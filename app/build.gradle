plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.garan.tempo"
        minSdkVersion 30
        targetSdkVersion 30
        versionCode 1
        versionName "0.1"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.0-alpha02"
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.garan.tempo'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.4'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
                kotlin {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    // Required for activity
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.activity:activity-compose:1.6.1"

    // Required for Wear Compose
    implementation "androidx.wear.compose:compose-foundation:$wear_compose_version"
    implementation "androidx.wear.compose:compose-material:$wear_compose_version"
    implementation "androidx.wear.compose:compose-navigation:$wear_compose_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha03"

    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    implementation "androidx.compose.foundation:foundation:1.4.0-alpha03"
    implementation "androidx.compose.material:material-icons-extended:1.4.0-alpha03"
    implementation "androidx.compose.ui:ui-tooling:1.4.0-alpha03"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.wear:wear:1.2.0"
    implementation "androidx.fragment:fragment-ktx:1.5.5"

    // Ongoing activity
    implementation "androidx.wear:wear-ongoing:1.0.0"

    // Required for the service
    implementation "androidx.lifecycle:lifecycle-service:2.5.1"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    kapt("androidx.hilt:hilt-compiler:1.0.0")
    implementation 'androidx.hilt:hilt-work:1.0.0'

    // For launching "About" via remote intent
    implementation "androidx.wear:wear-remote-interactions:1.0.0"

    // Health Services
    implementation "androidx.health:health-services-client:1.0.0-beta01"

    // For pagers
    implementation "com.google.accompanist:accompanist-pager:0.27.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.23.1"

    implementation "com.google.accompanist:accompanist-permissions:0.24.9-beta"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor("androidx.room:room-compiler:$room_version")
    kapt "androidx.room:room-compiler:$room_version"

    // Reflect
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Workmanager
    implementation("androidx.work:work-runtime:$work_version")
    implementation("androidx.work:work-runtime-ktx:$work_version")

    // For mapping images
    implementation("io.coil-kt:coil-compose:2.2.2")

    implementation "com.google.android.horologist:horologist-compose-layout:0.2.0"

    // Exercise cache
    implementation 'com.google.guava:guava:31.1-android'

    // FIT
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.google.protobuf:protobuf-kotlin-lite:3.21.9"
}